rem System variables are defined by the Piano Player app before this script is created and executed.
rem Those system variables are:
rem 	PianoPlayer_APP_DIRECTORY,
rem 	PianoPlayer_APP_UNINSTALLER_NAME,
rem 	PianoPlayer_SETUP_FILE_URL,
rem 	PianoPlayer_SETUP_FILE_NAME

rem >>> Make sure the script has been given a "key" argument to prevent the user from running this script
if not "%*"=="run_update" ( goto exit_fail_silent )

rem >>> Go to the temp directory where the update will be downloaded to
cd %temp%

rem >>> Check and make sure the system variables are defined before running the script
if not defined PianoPlayer_APP_DIRECTORY ( goto exit_fail )
if not defined PianoPlayer_APP_UNINSTALLER_NAME ( goto exit_fail )
if not defined PianoPlayer_SETUP_FILE_URL ( goto exit_fail )
if not defined PianoPlayer_SETUP_FILE_NAME ( goto exit_fail )

rem >>> Let the user know that the updater is running
rem >>> and wait a little bit before running the update, this will give Piano Player time to close itself
echo Piano Player update is starting, please do not close this window.
timeout 2 /nobreak > nul

rem >>> Remove the previous installer if there is one so that a new one can be downloaded
if exist "%PianoPlayer_SETUP_FILE_NAME%" (
del %PianoPlayer_SETUP_FILE_NAME% /f
if exist "%PianoPlayer_SETUP_FILE_NAME%" ( goto exit_fail )
)

rem >>> Execuute Windows Powershell and tell it to download the update file
echo Downloading update...
powershell -Command "(New-Object Net.WebClient).DownloadFile('%PianoPlayer_SETUP_FILE_URL%', '%PianoPlayer_SETUP_FILE_NAME%')"
timeout 2 /nobreak > nul

rem >>> Run the uninstaller after downloading the new version and wait for it to finish
echo Uninstalling older version so that the new one can be installed...
echo Please do not close this window.
start /w "" "%PianoPlayer_APP_DIRECTORY%\%PianoPlayer_APP_UNINSTALLER_NAME%" /SILENT /VERYSILENT /SUPPRESSMSGBOXES /NORESTART

rem >>> Check if the app uninstalled succesfully. If not, quit the updater and delete this script.
if not %errorlevel%==0 ( goto exit_fail )

rem >>> Execute the downloaded update file
echo Installing the latest version...
start /w "" %PianoPlayer_SETUP_FILE_NAME% /SILENT /VERYSILENT /SUPPRESSMSGBOXES /CURRENTUSER /NOCANCEL /NORESTART /CLOSEAPPLICATIONS /FORCECLOSEAPPLICATIONS /RESTARTAPPLICATIONS

rem >>> Check if the app installed succesfully. If not, delete the downloaded installer and this script.
if not %errorlevel%==0 (
del %PianoPlayer_SETUP_FILE_NAME% /f
goto exit_fail
)

echo Piano Player has been successfully updated.

rem >>> Exit and delete this bat script
:exit
timeout 2 /nobreak > nul
if exist "%PianoPlayer_SETUP_FILE_NAME%" ( del %PianoPlayer_SETUP_FILE_NAME% )
start /b "" cmd /c del "%~f0"&exit /b
exit /b 0

rem >>> Exit the script with error code 1, call when update fails
:exit_fail
echo Piano Player update has failed.
timeout 3 /nobreak > nul
if exist "%PianoPlayer_SETUP_FILE_NAME%" ( del %PianoPlayer_SETUP_FILE_NAME% /f )
start "" https://github.com/TheCSDev/piano-player/releases
start /b "" cmd /c del "%~f0"&exit /b 0
exit /b 1

:exit_fail_silent
rem >>> Silent exit with errorlevel 1
if exist "%PianoPlayer_SETUP_FILE_NAME%" ( del %PianoPlayer_SETUP_FILE_NAME% /f )
start /b "" cmd /c del "%~f0"&exit /b 0
exit /b 1
